apply plugin: 'com.android.application'

if (getGradle().getStartParameter().getTaskNames().toString().toLowerCase().contains("huawei")) {
    apply plugin: 'com.huawei.agconnect'
} else {
    apply plugin: 'com.google.gms.google-services'
}

def localProperties = new Properties()
try {
    localProperties.load(new FileInputStream(rootProject.file("local.properties")))
} catch (Exception ignored) {
    logger.warn("No Local Properties File Found!")
}
android {

    defaultConfig {
        applicationId "com.dimonvideo.client"
        minSdkVersion 23
        targetSdkVersion 35
        versionCode 300000073
        versionName "4.1"

        setProperty("archivesBaseName", applicationId + "-v" + versionCode)
        buildConfigField("String", "BASE64_ENCODED_PUBLIC_KEY", "\"" + localProperties['base64EncodedPublicKey'] + "\"")

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    compileSdkVersion = 35
    android.buildFeatures.buildConfig true
    flavorDimensions = ["dimonvideo"]

    productFlavors {
        DVClient_NashStore {
            dimension = 'dimonvideo'
            buildConfigField "boolean", "HUAWEI", "false"
            buildConfigField "boolean", "SAMSUNG", "false"
            buildConfigField "boolean", "NASHSTORE", "true"
            buildConfigField "boolean", "RUSTORE", "false"
            buildConfigField "boolean", "GOOGLE", "false"
            versionNameSuffix "-nashstore"
        }
        DVClient_RuStore {
            dimension = 'dimonvideo'
            buildConfigField "boolean", "HUAWEI", "false"
            buildConfigField "boolean", "SAMSUNG", "false"
            buildConfigField "boolean", "NASHSTORE", "false"
            buildConfigField "boolean", "RUSTORE", "true"
            buildConfigField "boolean", "GOOGLE", "false"
            versionNameSuffix "-rustore"
        }
        DVClient_Samsung {
            dimension = 'dimonvideo'
            buildConfigField "boolean", "HUAWEI", "false"
            buildConfigField "boolean", "SAMSUNG", "true"
            buildConfigField "boolean", "NASHSTORE", "false"
            buildConfigField "boolean", "RUSTORE", "false"
            buildConfigField "boolean", "GOOGLE", "false"
            versionNameSuffix "-samsung"
        }

        DVClient_Huawei {
            dimension = 'dimonvideo'
            buildConfigField "boolean", "HUAWEI", "true"
            buildConfigField "boolean", "SAMSUNG", "false"
            buildConfigField "boolean", "NASHSTORE", "false"
            buildConfigField "boolean", "RUSTORE", "false"
            buildConfigField "boolean", "GOOGLE", "false"
            versionNameSuffix "-huawei"
        }

        DVClient_Google {
            dimension = 'dimonvideo'
            buildConfigField "boolean", "HUAWEI", "false"
            buildConfigField "boolean", "SAMSUNG", "false"
            buildConfigField "boolean", "NASHSTORE", "false"
            buildConfigField "boolean", "RUSTORE", "false"
            buildConfigField "boolean", "GOOGLE", "true"
            versionNameSuffix "-google"
        }
    }
    namespace 'com.dimonvideo.client'
    sourceSets {
        main {
            java.srcDirs += 'src/main/java'
        }

        DVClient_NashStore {
            java.srcDirs += 'src/DVClient_NashStore/java'
            manifest.srcFile 'src/main/AndroidManifest.xml'
        }
        DVClient_RuStore {
            java.srcDirs += 'src/DVClient_RuStore/java'
            manifest.srcFile 'src/main/AndroidManifest.xml'
        }
        DVClient_Google {
            java.srcDirs += 'src/DVClient_Google/java'
            manifest.srcFile 'src/main/AndroidManifest.xml'
        }
        DVClient_Samsung {
            java.srcDirs += 'src/DVClient_Samsung/java'
            manifest.srcFile 'src/main/AndroidManifest.xml'
        }
        DVClient_Huawei {
            manifest.srcFile 'src/DVClient_Huawei/AndroidManifest.xml'
            java.srcDirs += 'src/DVClient_Huawei/java'
        }
    }
    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi', 'armeabi-v7a', 'mips', 'mips64', 'arm64-v8a'
            universalApk true
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'androidx.navigation:navigation-fragment:2.8.7'
    implementation 'androidx.navigation:navigation-ui:2.8.7'
    implementation 'androidx.preference:preference:1.2.1'
    implementation(platform('org.jetbrains.kotlin:kotlin-bom:1.9.23'))
    implementation 'androidx.activity:activity:1.10.0'

    implementation 'com.android.volley:volley:1.2.1'
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'

    implementation 'org.greenrobot:eventbus:3.3.1'
    implementation 'com.github.HamidrezaAmz:MagicalExoPlayer:3.0.8'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.github.omfine:imagePicker:v1.0.0'

    // Google
    DVClient_GoogleImplementation('com.google.firebase:firebase-messaging:24.1.0')

    // RuStore
    DVClient_RuStoreImplementation('com.google.firebase:firebase-messaging:24.1.0')

    // NashStore
    DVClient_NashStoreImplementation('com.google.firebase:firebase-messaging:24.1.0')

    // Samsung
    DVClient_SamsungImplementation('com.google.firebase:firebase-messaging:24.1.0')

    // HUAWEI
    DVClient_HuaweiImplementation 'com.huawei.agconnect:agconnect-core:1.9.1.301'
    DVClient_HuaweiImplementation 'com.huawei.hms:hianalytics:6.12.0.300'
    DVClient_HuaweiImplementation 'com.huawei.hms:push:6.11.0.300'

    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.3")
}